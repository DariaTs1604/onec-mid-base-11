
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++ Цвелева Дарья 08.10.2025 Добавление поля ввода контактных лиц на форму 	
	ПолеВводаКЛ = Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Элементы.ГруппаШапкаПраво);
	ПолеВводаКЛ.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВводаКЛ.ПутьКДанным = "Объект.ИТ_КонтактноеЛицо";  
	//--
	
	//++ Цвелева Дарья 08.10.2025 Добавление поле ввода для значения скидки,
	//команды и кнопки по пересчету таблицы с учетом указанной скидки 
	ГруппаСкидка = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.Отображение = ОтображениеОбычнойГруппы.Нет;
	
	ПолеВводаСкидка = Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Элементы.ГруппаСкидка);
	ПолеВводаСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВводаСкидка.ПутьКДанным = "Объект.ИТ_СогласованнаяСкидка";
	ПолеВводаСкидка.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении"); 
	
	КомандаПересчета = Команды.Добавить("ПересчитатьТаблицу");
	КомандаПересчета.Заголовок = "Пересчитать таблицу";
	КомандаПересчета.Действие = "ПересчитатьТаблицы"; 
	
	КнопкаФормыПересчет = Элементы.Добавить("ПересчитатьТаблицу", Тип("КнопкаФормы"), Элементы.ГруппаСкидка);
	КнопкаФормыПересчет.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормыПересчет.Заголовок = "Пересчитать таблицу";
	КнопкаФормыПересчет.Отображение = ОтображениеКнопки.КартинкаИТекст;
	КнопкаФормыПересчет.Картинка = БиблиотекаКартинок.Перечитать;
	КнопкаФормыПересчет.ПоложениеКартинки = ПоложениеКартинкиКнопкиФормы.Лево;
	//--
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//++ Цвелева Дарья 08.10.2025 Изменение формулы расчета суммы строки ТЧ
	//Исходный код:	
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки; 
	
	СуммарнаяСкидка = Объект.ИТ_СогласованнаяСкидка + ТекущиеДанные.Скидка;
	
	Если СуммарнаяСкидка <= 100 Тогда
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * (100 - (СуммарнаяСкидка)) / 100;
	ИначеЕсли СуммарнаяСкидка > 100 Тогда
		ПоказатьПредупреждение(, "Скидка не может превышать 100%!",, "Превышение скидки");
		ТекущиеДанные.Сумма = 0;
	КонецЕсли;
	
	РассчитатьСуммуДокумента();
	//--
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьТаблицы()  
	
	//++ Цвелева Дарья 20.08.2025 Процедура пересчета таблиц с учетом указанной скидки
	Для Каждого Строка Из Объект.Товары Цикл
		РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	
	Для Каждого Строка Из Объект.Услуги Цикл
		РассчитатьСуммуСтроки(Строка);
	КонецЦикла;
	//--  
	
КонецПроцедуры 
         
&НаКлиенте
Процедура СогласованнаяСкидкаПриИзменении(Элемент)
	
	//++ Цвелева Дарья 20.08.2025 Вопрос пользователю при изменении скидки, при условии пустых таблиц
	Если Объект.Товары.Количество() <> 0 ИЛИ Объект.Услуги.Количество() <> 0 Тогда		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект),
		"Изменен процент скидки. Пересчитать таблицы товаров и услуг?",РежимДиалогаВопрос.ДаНет);		
	КонецЕсли;
	//--
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	//++ Цвелева Дарья 20.08.2025 Положительный ответ пользователя на вопрос о пересчете таблицы	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда 
		ПересчитатьТаблицы();
	КонецЕсли;
	//--	
КонецПроцедуры 

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
